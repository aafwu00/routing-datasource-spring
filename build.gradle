buildscript {
    ext {
        springBootVersion = '1.5.17.RELEASE'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'http://repo.spring.io/plugins-release' }
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath 'com.netflix.nebula:nebula-project-plugin:5.1.2'
        classpath 'com.netflix.nebula:nebula-release-plugin:8.0.3'
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:4.0.1'
        classpath 'com.netflix.nebula:gradle-lint-plugin:10.1.2'

        classpath 'gradle.plugin.com.dorongold.plugins:task-tree:1.3'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'

        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE'
        classpath 'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
    }
}

allprojects {
    apply plugin: 'nebula.release'
    apply plugin: 'nebula.info'
    apply plugin: 'nebula.project'
    apply plugin: 'nebula.optional-base'
    apply plugin: 'nebula.lint'

    group 'com.github.aafwu00'

    contacts {
        'aafwu00@gmail.com' {
            moniker 'Taeho Kim'
            role 'Developer'
        }
    }
}

subprojects {
    apply plugin: 'java'

    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    test {
        useJUnitPlatform()
    }

    if (!project.name.contains('sample')) {
        apply plugin: 'maven-publish'
        apply plugin: 'checkstyle'
        apply plugin: 'findbugs'
        apply plugin: 'pmd'
        apply plugin: 'jacoco'

        apply plugin: 'com.dorongold.task-tree'
        apply plugin: 'com.github.ben-manes.versions'

        apply plugin: 'nebula.maven-publish'
        apply plugin: 'nebula.source-jar'

        bootRun.enabled = false
        bootRepackage.enabled = false
        
        dependencyUpdates.resolutionStrategy = {
            componentSelection { rules ->
                rules.all { ComponentSelection selection ->
                    boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'dev', 'snapshot'].any { qualifier ->
                        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                    }
                    if (rejected) {
                        selection.reject('Release Candidate')
                    }
                }
            }
        }

        checkstyle {
            toolVersion = '8.13'
            ignoreFailures = false
            maxErrors = 0
            maxWarnings = 0
            configFile = rootProject.file('config/checkstyle/checkstyle_checks.xml')
            configProperties = ['checkstyle.header.file'       : "${rootProject.projectDir}/config/checkstyle/java.header",
                                'checkstyle.regexp.header.file': "${rootProject.projectDir}/config/checkstyle/java_regexp.header",
                                'checkstyle.suppressions.file' : "${rootProject.projectDir}/config/checkstyle/suppressions.xml",
                                'checkstyle.importcontrol.file': "${rootProject.projectDir}/config/checkstyle/import-control.xml"]
        }

        findbugs {
            toolVersion = '3.0.1'
            ignoreFailures = false
            effort = 'max'
            reportLevel = 'high'
        }

        pmd {
            toolVersion = '6.8.0'
            ruleSetFiles = rootProject.files('config/pmd/rulesets.xml')
            sourceSets = [project.sourceSets.main as SourceSet]
            ignoreFailures = false
            rulePriority = 3
        }

        jacoco {
            toolVersion = '0.8.2'
        }
    }

    if (project.name.endsWith('sample')) {
        apply plugin: 'application'
    }

    if (project.name.contains('samples')) {
        bootRun.enabled = false
        bootRepackage.enabled = false
    }


    if (project.name.contains('autoconfigure')) {
        apply plugin: 'propdeps'
        apply plugin: 'propdeps-maven'
        apply plugin: 'propdeps-idea'
        apply plugin: 'propdeps-eclipse'

        compileJava.dependsOn(processResources)
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.1'
        testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.1'

        testCompile 'com.github.sbrannen:spring-test-junit5:1.3.1'
        testCompile 'org.springframework.boot:spring-boot-starter-test'

        testCompile 'org.assertj:assertj-core:3.11.1'

        if (project.name.contains('autoconfigure')) {
            annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        }
    }
}
